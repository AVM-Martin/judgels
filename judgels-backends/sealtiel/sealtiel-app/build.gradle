sourceSets {
    integTest {
        java {
            compileClasspath += main.output + test.output + project.configurations.annotationProcessor
            runtimeClasspath += main.output + test.output
            srcDir file('src/integTest/java')
        }
        resources.srcDir file('src/integTest/resources')
    }
}

configurations {
    integTestCompile.extendsFrom testCompile
    integTestRuntime.extendsFrom testRuntime
}

idea.module {
    scopes.TEST.plus += [configurations.integTestRuntimeClasspath, configurations.integTestCompileClasspath]

    testSourceDirs += file('src/integTest/java')
    testSourceDirs += file('src/integTest/resources')

    iml.withXml {
        def content = it.asNode().component.find { it.'@name' == 'NewModuleRootManager' }.content[0]
        content.sourceFolder.each { sourceFolder ->
            if (sourceFolder.@url?.endsWith('/resources')) {
                sourceFolder.attributes().with {
                    boolean isTestSource = (remove('isTestSource') == 'true')
                    put('type', isTestSource ? 'java-test-resource' : 'java-resource')
                }
            }
        }
    }
}

task integTest(type: Test) {
    group 'Verification'
    description 'Runs the integration tests.'

    dependencies {
        testImplementation "org.junit.jupiter:junit-jupiter-api:$junitJupiterVersion"
        testRuntime "org.junit.jupiter:junit-jupiter-engine:$junitJupiterVersion"
    }

    testClassesDirs = sourceSets.integTest.output.classesDirs
    classpath = sourceSets.integTest.runtimeClasspath

    check.dependsOn integTest
}

dependencies {
    compile project(':sealtiel:sealtiel-api')
    compile project(':judgels-commons:judgels-service-core')

    compile "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    compile "com.google.dagger:dagger:$daggerVersion"
    compile "com.palantir.remoting3:jersey-servers:$httpRemotingVersion"
    compile "com.rabbitmq:amqp-client:$rabbitmqAmqpClientVersion"
    compile "io.dropwizard:dropwizard-core:$dropwizardVersion"

    testCompile "org.assertj:assertj-core:$assertJVersion"
    testCompile "org.mockito:mockito-core:$mockitoVersion"

    integTestCompile "com.palantir.remoting3:jaxrs-clients:$httpRemotingVersion"
    integTestCompile "org.junit.jupiter:junit-jupiter-api:$junitJupiterVersion"
    integTestCompile "org.testcontainers:testcontainers:$testContainersVersion"
    integTestCompile "io.dropwizard:dropwizard-testing:$dropwizardVersion"

    compileOnly "com.google.dagger:dagger-compiler:$daggerVersion"
    compileOnly "org.immutables:value:$immutablesVersion"

    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"
    annotationProcessor "org.immutables:value:$immutablesVersion"
}
